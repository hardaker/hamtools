#!/usr/bin/perl

our $VERSION = '0.5';

# NEC info:
# http://www.nec2.org/part_3/toc.html

my $mode = 'comments';

my %defines;
my %configs = (
	       segments => 20,
	       radius => 0.3048,
	      );;
my %tags;
my %coords;

use strict;

my $tagnum;
my $lastcoord;
my %haveseen;

while(<>) {
    next if (/^#/ || /^\s*$/);

    #
    # generic top level things we allow
    #

    # define WORD DEFINITION
    #   or
    # define WORD {
    #   ...
    # }
    if (/^\s*define\s+(\w+)\s+(.*)/) {
	my ($word, $value) = ($1, $2);

	if ($value eq '{') {
	    # XXX: use @define foo {
	    # ...
	    # }
	    while (<>) {
		if (/^\s*}\s*$/) {
		    chomp($defines{$word});
		    last;
		}
		$defines{$word} .= $_;
	    }
	} else {
	    chomp($value);
	    $defines{$word} = $value;
	}

	next;
    }

    # config TOKEN VALUE
    if (/^\s*config\s+\(\w\)\s+(.*)/) {
	$configs{$1} .= $2;
	next;
    }

    # delete leading whitespace
    s/^\s+//;

    # beginning comments (CM -> CE)
    if ($mode eq 'comments' && !/^(CM|CE)/) {
	$mode = 'geometry';
	print "CM Output created by ezrnec\n";
	print "CE http://www.hamtools.org/\n";
    } elsif ($mode eq 'comments' && /^CE/) {
	print;
	next;
    }

    # invoke any defines and specials
    while (1) {
	#	    print "begin: $_\n";

	# definitions
	next if s/\@{(\w+)}/$defines{$1} || $tags{$1} || die("no such define or tag: '$1'")/e;

	# math
	next if s/\${([^\}]+)}/eval "$1"/e;

	# scaled by feet, cm, etc
	next if s/([-\d\.]+)(ft|m|cm|mm)/scale_dim($1, $2)/e;

	#	    print "final: $_\n";
	# nothing left
	last;
    }

    if (/^\s*output(.*)/) {
	print STDERR $1,"\n";
	next;
    }

    # geometry section
    if ($mode eq 'geometry' && /^GE/) {
	$mode eq 'control';
	print;
    } elsif ($mode eq 'geometry') {
	# process geometry tokens

	# wire definitions
	if (/^\s*(wire|cont)\s+(.*)/) {
	    my $line = $2;
	    my %params = %configs;
	    while ($line =~ s/^(\w+)=(\w+)\s+//) {
		$params{$1} = $2;
	    }

	    $tagnum++;

	    # memorize a tag if needed
	    if ($params{'tag'}) {
		$tags{$params{'tag'}} = $tagnum;
	    }

	    # remove commas
	    $line =~ s/,/ /g;

	    my $continfo;
	    if (/^cont/) {
		my $coordtouse = $lastcoord;
		$coordtouse = $coords{$params{'from'}} if ($params{'from'});
		$line = $coordtouse . " " . $line;
	    }

	    # rememeber the last coordinate
	    ($lastcoord) = ($line =~ /([-\.\d]+\s+[-\.\d]+\s+[-\.\d]+)$/);
#	    print "last coord:  $lastcoord\n";
	    if ($params{'tag'}) {
		$coords{$params{'tag'}} = $lastcoord;
	    }

            # XXX: proper printf for spacing
	    print "GW $tagnum $params{'segments'} $line $params{'radius'}\n";
	} elsif (/^\s*(GE|end)/) {
	    print "GE\n";
	    $mode = 'control';
	    next;
	} else {
	    /^\s*(\w+)/;
	    $haveseen{$1}++;
	    print;
	}
    }

#    print "mode:$mode / $_\n";
    if ($mode eq 'control') {
	if (/^\s*volt\s+(\w+)\s*(\d*)/) {
	    print "EX  0  $tags{$1}  " . ($2 || 1) . "\n";
	} elsif (/^\s*ground/) {
	    print "GN 1 0 0 0    0 0 0 0 0 0\n";
	} elsif (/^\s*freq(|ency)\s+([\.\d]+)[-\s]+([\.\d]+)\s*([\.\d]*)/) {
	    my ($bogus, $start, $stop, $steps) = ($1, $2, $3, $4);
	    $steps ||= 20;
	    print "FR 0 $steps  0 0 $start   " . 
	      (($stop - $start)/$steps) . "\n";
	} elsif (/^\s*EN/) {
	    $mode = 'end';
	} else {
	    /^\s*(\w+)/;
	    $haveseen{$1}++;
	    print;
	}
    }
}

if (!$haveseen{'RP'}) {
    print "RP     0    19    73   1000  0  0.00000E+00  5.00000E+00  5.00000E+00  0.00000E+00  0.00000E+00\n";
}

if ($mode eq 'control') {
    print "EN\n";
}

sub scale_dim {
    my ($len, $unit) = @_;
    return $len * .3048 if ($unit eq 'ft');
    return $len/100  	if ($unit eq 'cm');
    return $len/1000 	if ($unit eq 'mm');
    return $len      	if ($unit eq 'm');
    die "don't know what unit $unit is\n";
}

=head1 NAME

ezrnec - Create antennas using a much simpler file format

=head1 SYNOPSIS


=head1 DESCRIPTION

B<ezrnec> is designed to greatly simplify describing antennas.  The
I<NEC> modelling language is defined using a "card" format that is
fairly strict in its structure and requires far too much user thinking
as data is fed into it.  The B<ezrnec> input format is merely a
simplier and more intuitive language to describe antennas in.  The language

=cut

